// BSD 2-Clause License
//
// Copyright (c) 2022 Alasdair Armstrong
//
// All rights reserved.
//
// Redistribution and use in source and binary forms, with or without
// modification, are permitted provided that the following conditions are
// met:
//
// 1. Redistributions of source code must retain the above copyright
// notice, this list of conditions and the following disclaimer.
//
// 2. Redistributions in binary form must reproduce the above copyright
// notice, this list of conditions and the following disclaimer in the
// documentation and/or other materials provided with the distribution.
//
// THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
// "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
// LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
// A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
// HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
// SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
// LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
// DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
// THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
// (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
// OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

use id_arena::Arena;

use crate::ast::*;
use crate::lexer::*;

use lalrpop_util::ParseError;

grammar<'input>(arena: &mut Arena<Spanned<Exp>>, symtab: &mut Symtab<'input>);

Spanned<T>: Spanned<T> = {
    <start:@L> <x:T> <end:@R> => Spanned { node: x, span: (start, end) }
}

Comma<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T> => {
        v.push(e);
        v
    }
};

CommaNonEmpty<T>: Vec<T> = {
    <mut v:(<T> ",")+> <e:T> => {
        v.push(e);
        v
    }
};

Arg: Option<ExpId> = {
    <x:Exp> => Some(x),
    "_" => None,
}

U32: u32 = {
    <start:@L> <n:"natural"> <end:@R> =>? u32::from_str_radix(n, 10).map_err(|e| ParseError::User {
        error: ModelParseError::ParseInt { error: e, span: (start, end) }
    }),
}

FixedNat: Vec<bool> = {
    <start:@L> <n:"fixed_natural"> <end:@R> =>? {
        let (value, width) = n.split_once('i').unwrap();
        let value = u64::from_str_radix(n, 10).map_err(|e| ParseError::User {
            error: ModelParseError::ParseInt { error: e, span: (start, end) }
        })?;
        let width = usize::from_str_radix(n, 10).map_err(|e| ParseError::User {
            error: ModelParseError::ParseInt { error: e, span: (start, end) }
        })?;
        Ok(bits_from_u64(value, width))
    }
}

Ctor: Vec<Accessor> = {
    <id:Id> "=>" <mut a:FieldAccessor> => {
        a.push(Accessor::Ctor(id));
        a
    },
    "_" "=>" <mut a:FieldAccessor> => {
        a.push(Accessor::Wildcard);
        a
    },
}

FieldAccessor: Vec<Accessor> = {
    <a:Accessor> => a,
    <hex:"hex"> => vec![Accessor::Bits(bits_from_str(hex).unwrap())],
    <bin:"bin"> => vec![Accessor::Bits(bits_from_str(bin).unwrap())],
}

Accessor: Vec<Accessor> = {
    "." <a:DotAccessor> => a,
    "[" <hi:U32> ".." <lo:U32> "]" <a:(<Accessor>)?> => {
        match a {
            None => vec![Accessor::Subvec(hi, lo)],
            Some(mut a) => {
                a.push(Accessor::Subvec(hi, lo));
                a
            },
        }
    },
}

#[inline]
FieldName: Name = {
    <field:Id> => field,
    "address" => constants::ADDRESS.name(),
    "data" => constants::DATA.name(),
}

DotAccessor: Vec<Accessor> = {
    "match" "{" <mut ctors:CommaNonEmpty<Ctor>> "}" => {
        let mut acc = Vec::new();
        for ctor in ctors.iter_mut() {
            acc.append(ctor)
        }
        acc.push(Accessor::Match);
        acc
    },
    <field:FieldName> <a:(<Accessor>)?> => {
        match a {
            None => vec![Accessor::Field(field)],
            Some(mut a) => {
                a.push(Accessor::Field(field));
                a
            },
        }
    },
    "extz" "(" <n:U32> ")" <a:(<Accessor>)?> => {
        match a {
            None => vec![Accessor::Extz(n)],
            Some(mut a) => {
                a.push(Accessor::Extz(n));
                a
            },
        }
    },
    "exts" "(" <n:U32> ")" <a:(<Accessor>)?> => {
        match a {
            None => vec![Accessor::Exts(n)],
            Some(mut a) => {
                a.push(Accessor::Exts(n));
                a
            },
        }
    },
    "length" "(" <n:U32> ")" <a:(<Accessor>)?> => {
        match a {
            None => vec![Accessor::Length(n)],
            Some(mut a) => {
                a.push(Accessor::Length(n));
                a
            },
        }
    },
    "address" "(" ")" <a:(<Accessor>)?> => {
        match a {
            None => vec![Accessor::Address],
            Some(mut a) => {
                a.push(Accessor::Address);
                a
            },
        }
    },
    "data" "(" ")" <a:(<Accessor>)?> => {
        match a {
            None => vec![Accessor::Data],
            Some(mut a) => {
                a.push(Accessor::Data);
                a
            },
        }
    },
    <n:U32> <a:(<Accessor>)?> => {
        match a {
            None => vec![Accessor::Tuple(n)],
            Some(mut a) => {
                a.push(Accessor::Tuple(n));
                a
            }
        }
    }
}

AtomicExp: ExpId = {
    <start:@L> <hex:"hex"> <end:@R> => arena.alloc(Spanned { node: Exp::Bits(bits_from_str(hex).unwrap()), span: (start, end) }),
    <start:@L> "{" "}" <end:@R> => arena.alloc(Spanned { node: Exp::Empty, span: (start, end) }),
    <id:Spanned<Id>> => arena.alloc(Spanned { node: Exp::Id(id.node), span: id.span }),
    <start:@L> <id:Id> "(" <args:Comma<Arg>> ")" <end:@R> =>
        arena.alloc(Spanned { node: Exp::App(id, args), span: (start, end) }),
    "(" <x:Exp> ")" => x,
    <start:@L> "(" <xs:CommaNonEmpty<Exp>> ")" <end:@R> =>
        arena.alloc(Spanned { node: Exp::Tuple(xs), span: (start, end) }),
    <start:@L> "[" <x:Exp> "]" <end:@R> =>
        arena.alloc(Spanned { node: Exp::Unary(Unary::Identity, x), span: (start, end) }),
}

PrefixExp: ExpId = {
    <x:AtomicExp> => x,
    <start:@L> "~" <x:PrefixExp> <end:@R> =>
        arena.alloc(Spanned { node: Exp::Unary(Unary::Compl, x),  span: (start, end) }),
}

PostfixExp: ExpId = {
    <x:PrefixExp> => x,
    <start:@L> <x:AtomicExp> <accessor:Accessor> <end:@R> =>
        arena.alloc(Spanned { node: Exp::Accessor(x, accessor), span: (start, end) }),
    <start:@L> <x:PostfixExp> "?" <end:@R> => 
        arena.alloc(Spanned { node: Exp::Unary(Unary::IdentityUnion, x),  span: (start, end) }),
    <start:@L> <x:PostfixExp> "^-1" <end:@R> => 
        arena.alloc(Spanned { node: Exp::Unary(Unary::Inverse, x),  span: (start, end) }),
    <start:@L> <x:PostfixExp> "^*" <end:@R> => 
        arena.alloc(Spanned { node: Exp::Unary(Unary::TClosure, x),  span: (start, end) }),
    <start:@L> <x:PostfixExp> "^+" <end:@R> => 
        arena.alloc(Spanned { node: Exp::Unary(Unary::RTClosure, x),  span: (start, end) }),
}

Exp0: ExpId = {
    <x:PostfixExp> => x,
}

Exp1: ExpId = {
    <x:Exp0> => x,
    <start:@L> <x:Exp0> "*" <y:Exp0> <end:@R> =>
        arena.alloc(Spanned { node: Exp::Binary(Binary::Cartesian, x, y), span: (start, end) }),
}

Exp2: ExpId = {
    <x:Exp1> => x,
    <start:@L> <x:Exp2> "\\" <y:Exp1> <end:@R> =>
        arena.alloc(Spanned { node: Exp::Binary(Binary::Diff, x, y), span: (start, end) }),
}

Exp3: ExpId = {
    <x:Exp2> => x,
    <start:@L> <x:Exp2> "&" <y:Exp3> <end:@R> =>
        arena.alloc(Spanned { node: Exp::Binary(Binary::Inter, x, y), span: (start, end) }),
}

Exp4: ExpId = {
    <x:Exp3> => x,
    <start:@L> <x:Exp3> ";" <y:Exp4> <end:@R> =>
        arena.alloc(Spanned { node: Exp::Binary(Binary::Seq, x, y), span: (start, end) }),
}

Exp5: ExpId = {
    <x:Exp4> => x,
    <start:@L> <x:Exp4> "|" <y:Exp5> <end:@R> =>
        arena.alloc(Spanned { node: Exp::Binary(Binary::Union, x, y), span: (start, end) }),
}

Exp6: ExpId = {
    <x:Exp5> => x,
    <start:@L> <x:Exp5> "in" <y:Exp5> <end:@R> =>
        arena.alloc(Spanned { node: Exp::Binary(Binary::In, x, y), span: (start, end) }),
    <start:@L> <x:Exp5> "==" <y:Exp5> <end:@R> =>
        arena.alloc(Spanned { node: Exp::Binary(Binary::Eq, x, y), span: (start, end) }),
    <start:@L> <x:Exp5> "!=" <y:Exp5> <end:@R> =>
        arena.alloc(Spanned { node: Exp::Binary(Binary::Neq, x, y), span: (start, end) }),
}

Exp: ExpId = {
    <x:Exp6> => x,
    <start:@L> "set" <p:Id> <pty:TyAnnot> "=>" <x:Exp> <end:@R> =>
        arena.alloc(Spanned { node: Exp::Set(p, pty, x), span: (start, end) }),
    <start:@L> "relation" <p:Id> <pty:TyAnnot> "," <q:Id> <qty:TyAnnot> "=>" <x:Exp> <end:@R> =>
        arena.alloc(Spanned { node: Exp::Relation(p, pty, q, qty, x), span: (start, end) }),
    <start:@L> "forall" <ps:Comma<Param>> "=>" <x:Exp> <end:@R> =>
        arena.alloc(Spanned { node: Exp::Forall(ps, x), span: (start, end) }),
    <start:@L> "exists" <ps:Comma<Param>> "=>" <x:Exp> <end:@R> =>
        arena.alloc(Spanned { node: Exp::Exists(ps, x), span: (start, end) }),
}

#[inline]
TyAnnot: TyAnnot = {
  ":" <ty:AtomicExp> => Some(ty),
  => None,
}

Check: Check = {
    "acyclic" => Check::Acyclic,
    "irreflexive" => Check::Irreflexive,
    "empty" => Check::Empty,
    "~" "acyclic" => Check::NonAcyclic,
    "~" "irreflexive" => Check::NonIrreflexive,
    "~" "empty" => Check::NonEmpty,
}

Param: (Name, TyAnnot) = {
    <arg:Id> <ty:TyAnnot> => (arg, ty),
}

Def: Spanned<Def> = {
    <start:@L> "let" <f:Id> <ty:TyAnnot> "=" <x:Exp> <end:@R> =>
        Spanned { node: Def::Let(f, Vec::new(), ty, x), span: (start, end) },
    <start:@L> "let" <f:Id> "(" <params:Comma<Param>> ")" <ty:TyAnnot> "=" <x:Exp> <end:@R> =>
        Spanned { node: Def::Let(f, params, ty, x), span: (start, end) },
    <start:@L> <check:Check> <x:Exp> "as" <id:Id> <end:@R> =>
        Spanned { node: Def::Check(check, x, id), span: (start, end) },
    <start:@L> "assert" <x:Exp> <end:@R> =>
        Spanned { node: Def::Assert(x), span: (start, end) },
}

pub MemoryModel: MemoryModel = {
    <defs:Def*> => MemoryModel::new(defs)
}

Id: Name = <id:"id"> => symtab.intern(id);

extern {
    type Location = usize;
    type Error = ModelParseError;

    enum Tok<'input> {
        "id" => Tok::Id(<&'input str>),
        "string" => Tok::String(<&'input str>),
        "bin" => Tok::Bin(<&'input str>),
        "hex" => Tok::Hex(<&'input str>),
        "natural" => Tok::Nat(<&'input str>),
        "fixed_natural" => Tok::FixedNat(<&'input str>),
        "in" => Tok::In,
        "include" => Tok::Include,
        "as" => Tok::As,
        "acyclic" => Tok::Acyclic,
        "assert" => Tok::Assert,
        "flag" => Tok::Flag,
        "irreflexive" => Tok::Irreflexive,
        "empty" => Tok::Empty,
        "let" => Tok::Let,
        "length" => Tok::Length,
        "and" => Tok::And,
        "show" => Tok::Show,
        "unshow" => Tok::Unshow,
        "match" => Tok::Match,
        "set" => Tok::Set,
        "relation" => Tok::Relation,
        "forall" => Tok::Forall,
        "exists" => Tok::Exists,
        "extz" => Tok::Extz,
        "exts" => Tok::Exts,
        "address" => Tok::Address,
        "data" => Tok::Data,
        "==" => Tok::EqEq,
        "!=" => Tok::Neq,
        "=>" => Tok::EqGt,
        "^-1" => Tok::Inverse,
        "^+" => Tok::HatPlus,
        "^*" => Tok::HatStar,
        "0" => Tok::Zero,
        "." => Tok::Dot,
        ".." => Tok::DotDot,
        "," => Tok::Comma,
        "=" => Tok::Eq,
        "~" => Tok::Tilde,
        "|" => Tok::Bar,
        "&" => Tok::Amp,
        "++" => Tok::PlusPlus,
        ";" => Tok::SemiColon,
        "\\" => Tok::Backslash,
        "+" => Tok::Plus,
        "*" => Tok::Star,
        "?" => Tok::Question,
        "{" => Tok::Lbrace,
        "}" => Tok::Rbrace,
        "(" => Tok::Lparen,
        ")" => Tok::Rparen,
        "[" => Tok::Lsquare,
        "]" => Tok::Rsquare,
        ":" => Tok::Colon,
        "_" => Tok::Underscore,
    }
}
